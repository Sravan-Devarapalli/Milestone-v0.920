-- =============================================
-- Author:		ThulasiRam.P
-- Create date: 02-17-2012
-- Description: Delete 	Substitute Day of an company holiday 
-- =============================================
CREATE PROCEDURE [dbo].[DeleteSubstituteDay]
(
	@PersonId   INT,
	@SubstituteDayDate DATETIME ,
	@UserLogin	NVARCHAR(255)
)
AS
BEGIN
	SET NOCOUNT ON;
	
	DECLARE @ParentHolidayDate DATETIME = NULL,
	        @ParentHolidayDescription NVARCHAR(255),
			@PTOTimeTypeId INT,
			@CurrentPMTime DATETIME,
			@HolidayTimeTypeId INT,
			@PTOChargeCodeId INT,
			@HolidayChargeCodeId INT,
			@IsW2SalaryPerson	BIT = 0,
			@ModifiedBy INT

	 SELECT @ParentHolidayDate = pc.Date, 
		   @ParentHolidayDescription = c.HolidayDescription
	 FROM dbo.PersonCalendar pc 
	 INNER JOIN dbo.Calendar c ON c.Date = pc.Date
	 WHERE pc.SubstituteDate = @SubstituteDayDate AND pc.PersonId = @PersonId


	SELECT @IsW2SalaryPerson = 1
	FROM dbo.Pay pay 
	INNER JOIN dbo.Timescale ts ON pay.Timescale = ts.TimescaleId  
	WHERE	pay.Person = @PersonId AND  ts.Name = 'W2-Salary' AND @ParentHolidayDate IS NOT NULL AND @ParentHolidayDate BETWEEN pay.StartDate AND ISNULL(pay.EndDate,dbo.GetFutureDate())

	SELECT  @CurrentPMTime = dbo.InsertingTime(),
			@PTOTimeTypeId = dbo.GetPTOTimeTypeId(),
			@HolidayTimeTypeId = dbo.GetHolidayTimeTypeId()
				
	SELECT @PTOChargeCodeId = Id FROM ChargeCode WHERE TimeTypeId = @PTOTimeTypeId
	SELECT @HolidayChargeCodeId = Id FROM ChargeCode WHERE TimeTypeId = @HolidayTimeTypeId
	SELECT @ModifiedBy = PersonId FROM Person WHERE Alias = @UserLogin
   

	BEGIN TRY
	BEGIN TRAN tran_DeleteSubstituteDay



	DELETE pc
	FROM dbo.PersonCalendar pc 
	WHERE (pc.SubstituteDate = @SubstituteDayDate AND pc.PersonId = @PersonId) OR
		  (pc.Date = @SubstituteDayDate AND pc.PersonId = @PersonId)

	--Delete holiday timetype  Entry from TimeEntry table for substitute date.
	--Delete From TimeEntryHours.
	DELETE TEH
	FROM TimeEntry TE 
	JOIN TimeEntryHours TEH ON TEH.TimeEntryId = TE.TimeEntryId
	WHERE  TE.PersonId = @PersonId AND TE.ChargeCodeId = @HolidayChargeCodeId AND TE.ChargeCodeDate = @SubstituteDayDate

	--Delete From TimeEntry.
	DELETE TE
	FROM TimeEntry TE 
	WHERE  TE.PersonId = @PersonId AND TE.ChargeCodeId = @HolidayChargeCodeId AND TE.ChargeCodeDate = @SubstituteDayDate


	 IF(@IsW2SalaryPerson = 1 AND @ParentHolidayDate IS NOT NULL)
	 BEGIN

	    INSERT  INTO [dbo].[TimeEntry]
		                 (  [PersonId],
							[ChargeCodeId],
							[ChargeCodeDate],
							[Note],
							[ForecastedHours],
							[IsCorrect],
							[IsAutoGenerated]
		                 )
		VALUES(@PersonId,@HolidayChargeCodeId,@ParentHolidayDate,@ParentHolidayDescription,0,1,1)

		INSERT INTO [dbo].[TimeEntryHours] 
								(   [TimeEntryId],
									[ActualHours],
									[CreateDate],
									[ModifiedDate],
									[ModifiedBy],
									[IsChargeable],
									[ReviewStatusId]
								)
		SELECT TE.TimeEntryId, 8,@CurrentPMTime,@CurrentPMTime,@ModifiedBy,0,2 /* Approved */
		FROM [dbo].[TimeEntry] AS TE 
		WHERE TE.PersonId = @PersonId AND TE.ChargeCodeDate = @ParentHolidayDate AND TE.ChargeCodeId = @HolidayChargeCodeId
		

	 END

	COMMIT TRAN tran_DeleteSubstituteDay
	END TRY
	BEGIN CATCH
		ROLLBACK TRAN tran_DeleteSubstituteDay
		
		DECLARE	 @ERROR_STATE	TINYINT
		,@ERROR_SEVERITY		TINYINT
		,@ERROR_MESSAGE		    NVARCHAR(2000)
		,@InitialTranCount		TINYINT

		SET	 @ERROR_MESSAGE		= ERROR_MESSAGE()
		SET  @ERROR_SEVERITY	= ERROR_SEVERITY()
		SET  @ERROR_STATE		= ERROR_STATE()
		RAISERROR ('%s', @ERROR_SEVERITY, @ERROR_STATE, @ERROR_MESSAGE)
	END CATCH

END
