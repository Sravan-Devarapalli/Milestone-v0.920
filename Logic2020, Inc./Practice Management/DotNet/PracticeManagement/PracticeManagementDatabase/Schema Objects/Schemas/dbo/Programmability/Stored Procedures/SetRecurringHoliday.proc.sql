CREATE PROCEDURE [dbo].[SetRecurringHoliday]
(
	@Id INT = NULL,
	@IsSet	BIT,
	@UserLogin NVARCHAR(255)
)
AS
BEGIN

DECLARE @Today DATETIME,
	@ModifiedBy INT,
	@HolidayTimeTypeId INT,
	@CurrentPMTime DATETIME,
	@PTOTimeTypeId INT

DECLARE @RecurringHolidaysDates TABLE( [Date] DATETIME, [Description] NVARCHAR(255), [Id] INT)

SELECT @Today = dbo.GettingPMTime(GETUTCDATE())
	, @HolidayTimeTypeId = dbo.GetHolidayTimeTypeId()
	, @CurrentPMTime = dbo.InsertingTime()
	, @PTOTimeTypeId = dbo.GetPTOTimeTypeId()

SELECT @ModifiedBy = PersonId
FROM Person
WHERE Alias = @UserLogin

	BEGIN TRY
	
	BEGIN TRANSACTION Tran_SetRecurringHoliday	
	
	EXEC SessionLogPrepare @UserLogin = @UserLogin

	--Set the value in CompanyRecurringHoliday
	UPDATE dbo.CompanyRecurringHoliday
	SET IsSet = @IsSet
	WHERE Id = @Id OR @Id IS NULL

	INSERT INTO @RecurringHolidaysDates([Date], [Description], [Id])
	SELECT C1.Date, crh.Description, crh.Id
	FROM dbo.Calendar AS C1
	JOIN dbo.CompanyRecurringHoliday crh ON C1.[Date] >= @Today
			AND 
			(
					(crh.[Day] IS NOT NULL --If holiday is on exact Date.
						AND (	--If Holiday comes in 
								DAY(C1.[Date]) = crh.[Day] AND MONTH(C1.[Date]) = crh.[Month] AND DATEPART(DW,C1.[Date]) NOT IN(1,7)
								OR DAY(DATEADD(DD,1,C1.[Date])) = crh.[Day] AND MONTH(DATEADD(DD,1,C1.[Date])) = crh.[Month]  AND DATEPART(DW,C1.[Date]) = 6
								OR DAY(DATEADD(DD,-1,C1.[Date])) = crh.[Day] AND MONTH(DATEADD(DD,-1,C1.[Date])) = crh.[Month] AND DATEPART(DW,C1.[Date]) = 2
							)
						)
						OR
						( crh.[Day] IS NULL AND MONTH(C1.[Date]) = crh.[Month]
						AND (
								DATEPART(DW,C1.[Date]) = crh.DayOfTheWeek
								AND (
										(crh.NumberInMonth IS NOT NULL
											AND  (C1.[Date] - DAY(C1.[Date])+1) -
														CASE WHEN (DATEPART(DW,C1.[Date]-DAY(C1.[Date])+1))%7 <= crh.DayOfTheWeek 
																THEN (DATEPART(DW,C1.[Date]-DAY(C1.[Date])+1))%7
																ELSE (DATEPART(DW,C1.[Date]-DAY(C1.[Date])+1)-7)
																END +(7*(crh.NumberInMonth-1))+crh.DayOfTheWeek = C1.[Date]
											)
											OR( crh.NumberInMonth IS NULL 
												AND (DATEADD(MM,1,C1.[Date] - DAY(C1.[Date])+1)- 1) - 
														(CASE WHEN DATEPART(DW,(DATEADD(MM,1,C1.[Date] - DAY(C1.[Date])+1)- 1)) >= crh.DayOfTheWeek
															THEN (DATEPART(DW,(DATEADD(MM,1,C1.[Date] - DAY(C1.[Date])+1)- 1)))-7
															ELSE (DATEPART(DW,(DATEADD(MM,1,C1.[Date] - DAY(C1.[Date])+1)- 1)))
															END)-(7-crh.DayOfTheWeek)= C1.[Date]
											)
										)
							)
						
						)
				 
				)	
	WHERE crh.Id = @Id OR @Id IS NULL

	--Update Calendar table.
	UPDATE  C1
	SET DayOff = @IsSet,
		IsRecurring = @IsSet,
		RecurringHolidayId = CASE WHEN @IsSet = 0 THEN null ELSE rhd.Id END,
		HolidayDescription = CASE WHEN @IsSet = 1 THEN rhd.Description
								ELSE NULL END,
		RecurringHolidayDate = NULL
	FROM dbo.Calendar AS C1
	JOIN @RecurringHolidaysDates rhd ON C1.Date = rhd.Date
	

	--Update PersonCalendarAuto table
	UPDATE C1
	SET DayOff = @IsSet
	FROM dbo.PersonCalendarAuto C1
	JOIN @RecurringHolidaysDates rhd ON C1.Date = rhd.Date
		
	--Delete TimeEntryHours
	DELETE TEH
	FROM TimeEntryHours TEH
	JOIN TimeEntry TE ON TE.TimeEntryId = TEH.TimeEntryId
	JOIN @RecurringHolidaysDates rhd ON rhd.Date = TE.ChargeCodeDate
	JOIN ChargeCode CC ON CC.Id = TE.ChargeCodeId AND CC.TimeTypeId IN (@PTOTimeTypeId, @HolidayTimeTypeId)
	
	--Delete TimeEntry.
	DELETE TE
	FROM TimeEntry TE
	JOIN @RecurringHolidaysDates rhd ON rhd.Date = TE.ChargeCodeDate
	JOIN ChargeCode CC ON CC.Id = TE.ChargeCodeId AND CC.TimeTypeId IN (@PTOTimeTypeId, @HolidayTimeTypeId)
				
	IF @IsSet = 1
	BEGIN

		INSERT  INTO [dbo].[TimeEntry]
		                ( [PersonId],
							[ChargeCodeId],
							[ChargeCodeDate],
							[Note],
							[ForecastedHours],
							[IsCorrect],
							[IsAutoGenerated]
		                )
		SELECT P.PersonId
				,CC.Id
				,rhd.[Date]
				,rhd.[Description]
				,0 --Forecasted Hours.
				,1
				,1 --Here it is Auto generated.
		FROM Person P
		JOIN Pay pay ON pay.Person = P.PersonId  AND pay.Timescale = 2 AND p.PersonId = pay.Person AND P.IsStrawman = 0
		JOIN @RecurringHolidaysDates AS rhd ON rhd.Date BETWEEN pay.StartDate AND (CASE WHEN p.TerminationDate IS NOT NULL AND pay.EndDate - 1 > p.TerminationDate THEN p.TerminationDate
																															ELSE pay.EndDate - 1
																															END)
		JOIN ChargeCode CC ON CC.TimeTypeId = @HolidayTimeTypeId
		LEFT JOIN TimeEntry TE ON TE.PersonId = P.PersonId AND TE.ChargeCodeId = CC.Id AND TE.ChargeCodeDate = rhd.Date
		WHERE TE.TimeEntryId IS NULL

		INSERT INTO [dbo].[TimeEntryHours] 
						( [TimeEntryId],
							[ActualHours],
							[CreateDate],
							[ModifiedDate],
							[ModifiedBy],
							[IsChargeable],
							[ReviewStatusId]
						)
		SELECT TE.TimeEntryId
				,8--Actual Hours
				,@CurrentPMTime
				,@CurrentPMTime
				,@ModifiedBy
				,0--Non Billable
				,1--Pending ReviewStatusId
		FROM Person P
		JOIN Pay pay ON pay.Person = P.PersonId  AND pay.Timescale = 2 AND p.PersonId = pay.Person AND P.IsStrawman = 0
		JOIN @RecurringHolidaysDates AS rhd ON rhd.Date BETWEEN pay.StartDate AND (CASE WHEN p.TerminationDate IS NOT NULL AND pay.EndDate - 1 > p.TerminationDate THEN p.TerminationDate
																															ELSE pay.EndDate - 1
																															END)
		JOIN ChargeCode CC ON CC.TimeTypeId = @HolidayTimeTypeId
		JOIN TimeEntry TE ON TE.PersonId = P.PersonId AND TE.ChargeCodeId = CC.Id AND TE.ChargeCodeDate = rhd.Date
		LEFT JOIN TimeEntryHours TEH ON TEH.TimeEntryId = TE.TimeEntryId
		WHERE TEH.TimeEntryId IS NULL
		
	END
	ELSE IF @IsSet = 0
	BEGIN

		INSERT  INTO [dbo].[TimeEntry]
		        ( [PersonId],
					[ChargeCodeId],
					[ChargeCodeDate],
					[Note],
					[ForecastedHours],
					[IsCorrect],
					[IsAutoGenerated]
		        )
		SELECT PC.PersonId,
				CC.Id,
				PC.Date,
				CASE WHEN PC.IsFloatingHoliday = 1 THEN 'Floating Holiday' ELSE 'PTO' END,
				0,
				1,
				1
		FROM PersonCalendar PC
		JOIN @RecurringHolidaysDates d ON d.date = PC.Date AND PC.DayOff = 1
		JOIN Person p ON p.PersonId = PC.PersonId AND P.IsStrawman = 0
		JOIN Pay pay ON pay.Person = PC.PersonId AND pay.Timescale = 2 AND d.date BETWEEN pay.StartDate AND (CASE WHEN p.TerminationDate IS NOT NULL AND pay.EndDate - 1 > p.TerminationDate THEN p.TerminationDate
																															ELSE pay.EndDate - 1
																															END)
		JOIN ChargeCode CC ON (PC.IsFloatingHoliday = 1 AND CC.TimeTypeId = @HolidayTimeTypeId) OR (PC.IsFloatingHoliday <> 1 AND CC.TimeTypeId = @PTOTimeTypeId)

		INSERT INTO [dbo].[TimeEntryHours] 
						( [TimeEntryId],
							[ActualHours],
							[CreateDate],
							[ModifiedDate],
							[ModifiedBy],
							[IsChargeable],
							[ReviewStatusId]
						)
		SELECT TE.TimeEntryId,
				CASE WHEN PC.ActualHours IS NOT NULL AND ISNULL(PC.IsFloatingHoliday,0) = 0 THEN PC.ActualHours ELSE 8 END,
				@CurrentPMTime,
				@CurrentPMTime,
				@ModifiedBy,
				0,--Non billable
				CASE WHEN PC.IsFromTimeEntry <> 1 AND PC.IsFloatingHoliday <> 1 THEN 2 ELSE 1 END --ReviewStatusId 2 is Approved, 1 is Pending.
		FROM PersonCalendar PC
		JOIN @RecurringHolidaysDates d ON d.date = PC.Date AND PC.DayOff = 1
		JOIN Person p ON p.PersonId = PC.PersonId AND P.IsStrawman = 0
		JOIN Pay pay ON pay.Person = PC.PersonId AND pay.Timescale = 2 AND d.date BETWEEN pay.StartDate AND (CASE WHEN p.TerminationDate IS NOT NULL AND pay.EndDate - 1 > p.TerminationDate THEN p.TerminationDate
																															ELSE pay.EndDate - 1
																															END)
		JOIN ChargeCode CC ON (PC.IsFloatingHoliday = 1 AND CC.TimeTypeId = @HolidayTimeTypeId) OR (PC.IsFloatingHoliday <> 1 AND CC.TimeTypeId = @PTOTimeTypeId)
		JOIN TimeEntry TE ON TE.ChargeCodeId = CC.Id AND TE.ChargeCodeDate = d.Date AND TE.PersonId = p.PersonId
		LEFT JOIN TimeEntryHours TEH ON TEH.TimeEntryId = TE.TimeEntryId
		WHERE TEH.TimeEntryId IS NULL
	END
	
		COMMIT TRANSACTION Tran_SetRecurringHoliday	
	END TRY
	BEGIN CATCH
		ROLLBACK TRANSACTION Tran_SetRecurringHoliday
		
		DECLARE	 @ERROR_STATE			tinyint
		,@ERROR_SEVERITY		tinyint
		,@ERROR_MESSAGE		    nvarchar(2000)
		,@InitialTranCount		tinyint

		SET	 @ERROR_MESSAGE		= ERROR_MESSAGE()
		SET  @ERROR_SEVERITY	= ERROR_SEVERITY()
		SET  @ERROR_STATE		= ERROR_STATE()
		RAISERROR ('%s', @ERROR_SEVERITY, @ERROR_STATE, @ERROR_MESSAGE)
	END CATCH

END

GO

